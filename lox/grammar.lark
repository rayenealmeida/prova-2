?start         : program

program        : stmt*

?stmt          : expr ";"
               | print_cmd
               | var_def
               | fun_def
               // | if_cmd
               // | for_cmd
               // | ...

print_cmd      : "print" expr ";"

// TIPAGEM
?type_hint     : ":" type_spec
type_spec      : CNAME ["?"]    -> type_nullable

// DECLARAÇÃO DE VARIÁVEL
var_def        : "var" CNAME type_hint? "=" expr ";"

// DECLARAÇÃO DE FUNÇÃO
fun_def        : "fun" CNAME "(" params_def ")" ["->" type_spec] block
params_def     : [param_def ("," param_def)*]
param_def      : CNAME type_hint?

block          : "{" stmt* "}"

// EXPRESSÕES
?expr          : assign

?assign        : ID "=" assign      -> assign_expr
               | test

?test          : test "==" sum      -> eq
               | test "!=" sum      -> ne
               | sum

?sum           : sum "+" term       -> add
               | sum "-" term       -> sub
               | term

?term          : term "*" factor    -> mul
               | term "/" factor    -> div
               | factor

?factor        : CNAME
               | NUMBER
               | BOOL
               | STRING
               | NIL
               | "(" expr ")"
               | call

call           : CNAME "(" params ")"
params         : [ expr ("," expr )* ]

// TOKENS
BOOL.2         : "true" | "false"
CNAME          : /[a-zA-Z_]\w*/
ID             : CNAME
NUMBER         : /([1-9][0-9]*|0)(\.[0-9]+)?/
STRING         : /"[^"]*"/
NIL.2          : "nil"
COMMENT        : "//" /[^\n]*/

%ignore /\s/ | COMMENT
